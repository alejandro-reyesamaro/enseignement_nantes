/* ex 2.1 pb de couverture d'ensemble  
   mod√®le implicite avec simplification du fait que nous faisons l'hypothèse que la matrice creuse est toujours composée de 1*/

# D√©claration des donn√©es du probl√®me

	param maxSalle;
	set S := 1..maxSalle; # index des salles

	set indCam; # index des cam√©ras
	
	set SCam within S cross indCam; # double-indice des salles et cam√©ras	

	param obj{indCam}; # Vecteur des coefficients de la fonction objectif (indic√© sur les cam√©ras)

	param coeffcontr{(i,j) in SCam}; # Matrice des coefficients d√©finissant les membres de gauche des contraintes (double-indice)

	param mdroite{S}; # Vecteur des membres de droite des contraintes (indic√© sur les salle)


# D√©claration d'un tableau de variables binaires
	
	var x{indCam} binary;	

# Fonction objectif

	minimize cout : sum{j in indCam} obj[j]*x[j];

# Contraintes

	s.t. Salle{i in S} : sum{(i,j) in SCam} coeffcontr[i,j] * x[j] >= mdroite[i]; 

# R√©solution (qui est ajout√©e en fin de fichier si on ne le pr√©cise pas)

	solve;

# Affichage des r√©sultats

	display : x;	# affichage "standard" des variables
	display{j in indCam : x[j] = 1} : j; # affichage plus lisible  
	display : "objectif : ", sum{j in indCam} obj[j]*x[j];

# donn√©es num√©riques dont le d√©but est indiqu√© par le mot-cl√© "data;"

data;

	param maxSalle := 10;
	
	set indCam := B C D E F G H I J K L M N O P Q R;

	set SCam := (1,B) (1,E) (1,F) (2,B) (2,C) (2,D) (3,D) (3,H) (3,I) (4,E) (4,G) (4,L) (4,M) (5,C) (5,F) (5,G) (5,H) (5,J) (5,K)
                (6,I) (6,J) (6,P) (7,M) (7,N) (8,K) (8,L) (8,N) (8,O) (8,R)	(9,O) (9,P) (9,Q) (10,Q) (10,R);

	param obj default 1;	

	param coeffcontr default 1;

	param mdroite default 1:= 4 2;
	
# Fin

end;

